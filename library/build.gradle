plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

group = 'org.dicekeys'
version = '1.0'

android {
    namespace = 'org.dicekeys.read'
    ndkVersion '26.2.11394342' // Latest LTS

    compileSdkVersion 34
    buildToolsVersion "32.0.0"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
            debug {
                debuggable true
                initWith debug
                jniDebuggable true
            }
        }

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"    
                //arguments "-DANDROID_STL=c++_shared"
                targets "jni-read-dicekey"

            }
        }
    }

    externalNativeBuild {
        cmake {
            version "3.15.0+"
            path file('src/main/cpp/CMakeLists.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    viewBinding.enabled = true
}

dependencies {
    /**  --- DiceKeys --------------------------------------------------------------------------- */
    implementation "com.github.dicekeys:seeded-crypto-android:$seeded_crypto"
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Android  --------------------------------------------------------------------------- */
    implementation "androidx.core:core-ktx:1.7.0"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    /** ----------------------------------------------------------------------------------------- */

    /**  --- Camera ----------------------------------------------------------------------------- */
    implementation "androidx.camera:camera-camera2:1.1.0-rc01"
    implementation "androidx.camera:camera-view:1.1.0-rc01"
    implementation "androidx.camera:camera-lifecycle:1.0.2"
    implementation 'com.google.zxing:core:3.4.1'
    /** ----------------------------------------------------------------------------------------- */

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'org.dicekeys'
                artifactId = 'read'
                version = '1.0'
            }
        }
    }
}